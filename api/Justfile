@_help:
    just --list

local_url := "http://localhost:8080"
bat := "bat --style plain --language json"
jq := "jq | " + bat

# Run the server.
start:
    #!/usr/bin/env zsh
    if ! http GET {{local_url}} &>/dev/null; then
        env EDGEDB_DSN="$( \
            edgedb instance credentials \
                --insecure-dsn \
                --instance mishpocha_db \
            )?tls_security=insecure" \
        cargo run &>/dev/null &
    fi

# Stop the server.
stop:
    #!/usr/bin/env zsh
    mishpocha="$(pgrep mishpocha-api)"
    if [ -n "${mishpocha}" ]; then
        kill "${mishpocha}" &>/dev/null
    fi

# View people in the database.
@people: start
    http {{local_url}}/people | {{jq}}

# View a person in the database.
@person full_name: start
    http "{{local_url}}/person?full_name={{full_name}}" | {{jq}}
