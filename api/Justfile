!include ../helpers.just

set fallback

cargo_run_with_db := '\
env EDGEDB_DSN="${EDGEDB_DSN}" \
    EDGEDB_CLIENT_TLS_SECURITY="insecure" \
    cargo run'
image := "mishpocha-api"
port := "8080"
url := "http://localhost:" + port

_help *_from-main:
    #!/usr/bin/env zsh
    if [ -z "{{_from-main}}" ]; then
        {{just}} --list
    else
        {{just}} --list --list-heading $'\nAPI: [`{{just}} api <COMMAND>`]\n'
    fi

# Install dependencies.
install *_from-main:
    #!/usr/bin/env zsh
    if [ -z "{{_from-main}}" ]; then
        ../install_dependencies.sh --api
    fi
    cargo build

# Update dependencies.
update *_from-main:
    #!/usr/bin/env zsh
    if [ -z "{{_from-main}}" ]; then
        ../install_dependencies.sh --api --update
    fi
    cargo update

# Check code for errors.
@check:
    cargo check

# Format code.
@format:
    cargo format

# Add dependencies.
@add *packages:
    cargo add {{packages}}

# Remove dependencies.
@remove *packages:
    cargo remove {{packages}}

# List dependencies.
@list:
    cargo tree --depth 1

_get_instance *instance:
    #!/usr/bin/env zsh
    {{get_instance}}

_build *prod:
    #!/usr/bin/env zsh
    if pgrep -q docker; then
        target={{ if prod =~ "--prod" { "prod" } else { "dev" } }}
        docker build --target "${target}" --tag {{image}} .
    fi

# Run the application (optional: "--docker", "--local", "--prod", "--open").
start *args: stop (_build args)
    #!/usr/bin/env zsh
    if [[ "{{args}}" = *"--docker"* ]]; then
        instance="--docker"
    elif [[ "{{args}}" = *"--local"* ]]; then
        instance="local"
    else
        instance=""
    fi
    instance="$({{just}} _get_instance ${instance})"
    {{ if args =~ "--open" { just + " open" } else { "" } }}
    if [ "${instance}" = "docker" ]; then
        if [ -z "$({{just}} running)" ]; then
            {{check_if_docker_running}}
        fi
        if [[ "{{args}}" = *"--prod"* ]]; then
            docker run \
                --detach \
                --name {{image}} \
                --publish {{port}}:{{port}} \
                --rm \
                {{image}}
        else
            docker run \
                --detach \
                --name {{image}} \
                --publish {{port}}:{{port}} \
                --rm \
                {{image}}
        fi
    else
        EDGEDB_DSN="$({{just}} db dsn)"
        {{cargo_run_with_db}}
    fi

# Stop the server.
stop:
    #!/usr/bin/env zsh
    process="$(pgrep -f {{image}})"
    if [ -n "${process}" ]; then
        kill "${process}"
    fi
    if [ -n "$({{just}} running)" ]; then
        docker kill {{image}}
    fi

# Remove the Docker image.
[no-exit-message]
@clean:
    docker rmi {{image}} >/dev/null 2>&1

# Show the container id (or all info with "--verbose") if the container is running.
running *verbose:
    #!/usr/bin/env zsh
    if pgrep -q docker; then
        docker container ls \
            {{ if verbose =~ "--verbose" { "" } else { "--quiet" } }} \
            --filter name={{image}}
    fi

# Show the Docker logs up to <lines> lines.
logs lines="20":
    #!/usr/bin/env zsh
    if [ -n "$({{just}} running)" ]; then
        docker logs {{image}} --tail {{lines}}
    fi

# Log into the interactive shell in Docker.
shell:
    #!/usr/bin/env zsh
    {{check_if_docker_running}}
    if [ -n "$({{just}} running)" ]; then
        {{just}} start
    fi
    docker exec --interactive --tty {{image}} bash

# Open the application in the browser.
@open:
    open {{url}}

# Ping the application.
@ping:
    http get {{url}}

# View people in the database.
@people:
    http {{url}}/people

# View a person in the database.
@person full_name:
    http "{{url}}/person?full_name={{full_name}}"
