@_help:
    just --list

local_url := "http://localhost:8080"
bat := "bat --style plain --language json"
jq := "jq | " + bat

# Run the server.
start:
    #!/usr/bin/env zsh
    if ! http GET {{local_url}} &>/dev/null; then
        env EDGEDB_DSN="$( \
            edgedb instance credentials \
                --insecure-dsn \
                --instance mishpocha_db \
            )?tls_security=insecure" \
        cargo run &>/dev/null &
    fi

# Stop the server.
stop:
    #!/usr/bin/env zsh
    mishpocha="$(pgrep mishpocha-api)"
    if [ -n "${mishpocha}" ]; then
        kill "${mishpocha}" &>/dev/null
    fi

# Seed the database with initial data.
seed: start
    #!/usr/bin/env zsh
    cd seed/date
    for file in $(ls); do
        http POST {{local_url}}/date < "${file}"
    done
    cd ../person
    for file in $(ls); do
        first_name="$(cat "${file}" | jq ".first_name")"
        last_name="$(cat "${file}" | jq ".last_name")"
        full_name="${first_name} ${last_name}"
        person="$(just person "${full_name}")"
        if [ "${person}" = "[]" ]; then
            http POST {{local_url}}/person < "${file}"
        fi
    done
    cd ../instrument
    for file in $(ls); do
        http POST {{local_url}}/instrument < "${file}"
    done
    cd ../player
    for file in $(ls); do
        http POST {{local_url}}/player < "${file}"
    done

# View people in the database.
@people: start
    http {{local_url}}/people | {{jq}}

# View people in the database.
person full_name: start
    #!/usr/bin/env zsh
    http "{{local_url}}/person?full_name={{full_name}}" | {{jq}}
