set fallback

cargo_run_with_db := 'env EDGEDB_DSN="${EDGEDB_DSN}" cargo run'
image := "mishpocha-api"
port := "8080"
url := "http://localhost:" + port
bat := "bat --style plain --language json"
jq := "jq | " + bat

_help *_from-main:
    #!/usr/bin/env zsh
    if [ -z "{{_from-main}}" ]; then
        just --list
    else
        just --list --list-heading $'\nAPI: [`just api <COMMAND>`]\n'
    fi

# Run the server locally.
local *args: stop
    #!/usr/bin/env zsh
    EDGEDB_DSN="$( \
        edgedb instance credentials \
            --insecure-dsn \
            --instance mishpocha_db \
        )?tls_security=insecure"
    if [[ "{{args}}" = *"--background"* ]]; then
        {{cargo_run_with_db}} &>/dev/null &
    else
        {{cargo_run_with_db}}
    fi

# Stop the server.
stop:
    #!/usr/bin/env zsh
    mishpocha="$(pgrep {{image}})"
    if [ -n "${mishpocha}" ]; then
        kill "${mishpocha}" &>/dev/null
    fi
    if [ -n "$(just running)" ]; then
        docker kill {{image}}
    fi

# Show if the container is running.
running:
    #!/usr/bin/env zsh
    if ps | grep docker | grep -v grep; then
        docker container ls --filter name={{image}}
    fi

_build *prod:
    #!/usr/bin/env zsh
    if [ -z "$(just running)" ]; then \
        target="{{ if prod =~ "--prod" { "prod" } else { "dev" } }}"
        docker build --target "${target}" --tag {{image}} .
    fi

# Open the application in the browser.
@open:
    open {{url}}

# Run the Docker container, and optionally "--open" in the browser.
start *args: stop (_build args)
    #!/usr/bin/env zsh
    if [[ "{{args}}" = *--prod* ]]; then
        docker run \
            --detach \
            --name {{image}} \
            --publish {{port}}:{{port}} \
            --rm \
            {{image}}
    else
        docker run \
            --detach \
            --name {{image}} \
            --publish {{port}}:{{port}} \
            --rm \
            --volume "$(pwd)"/src:/api \
            {{image}}
    fi
    {{ if args =~ "--open" { "just open" } else { "" } }}

# Show the Docker logs.
logs tail="20":
    #!/usr/bin/env zsh
    if [ -n "$(just _is_running)" ]; then
        docker logs {{image}} --tail {{tail}}
    fi

# Log into the interactive shell in Docker.
shell:
    #!/usr/bin/env zsh
    if [ -n "$(just running)" ]; then
        just start
    fi
    docker exec --interactive --tty {{image}} bash

# Remove the Docker image.
clean:
    #!/usr/bin/env zsh
    docker rmi {{image}} >/dev/null 2>&1
    exit 0

# View people in the database.
@people: local
    http {{url}}/people | {{jq}}

# View a person in the database.
@person full_name: local
    http "{{url}}/person?full_name={{full_name}}" | {{jq}}
