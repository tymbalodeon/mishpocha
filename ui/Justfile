!include ../helpers.just

set fallback

dev_port := "5173"
prod_port := "3000"
image := "mishpocha-ui"

_help *_from-main:
    #!/usr/bin/env zsh
    if [ -z "{{_from-main}}" ]; then
        {{just}} --list
    else
        {{just}} --list --list-heading $'\nUI: [`{{just}} ui <COMMAND>`]\n'
    fi

# Install dependencies.
install *_from-main:
    #!/usr/bin/env zsh
    if [ -z "{{_from-main}}" ]; then
        ../install_dependencies.sh --ui
    fi
    pnpm install

# Update dependencies.
update *_from-main:
    #!/usr/bin/env zsh
    if [ -z "{{_from-main}}" ]; then
        ../install_dependencies.sh --ui --update
    fi
    pnpm update

# Check code for errors.
check *files: format
    #!/usr/bin/env zsh
    if [ -n "{{files}}" ]; then
        pnpx rome check "{{files}}"
    else
        files=(src/**/*.ts*)
        pnpx rome check --apply "${files[@]}" \
            || pnpx rome check "${files[@]}"
    fi
    pnpm run build.types

# Format code.
@format:
    pnpx rome format --write .

# Add dependencies.
@add *packages:
    pnpm add {{packages}}

# Add dependencies to "dev" group.
@add-dev *packages:
    pnpm add --save-dev {{packages}}

# Remove dependencies.
@remove *packages:
    pnpm remove {{packages}}

# List dependencies.
@list:
    pnpm list

_get_instance *instance:
    #!/usr/bin/env zsh
    if [ -n "$(pgrep docker -q)" ] && [ -z "{{instance}}" ] \
        || [[ "{{instance}}" = *"--docker"* ]]; then
        echo "--docker"
    else
        echo "--local"
    fi

_build *prod:
    #!/usr/bin/env zsh
    if pgrep -q docker; then
        target="{{ if prod =~ "--prod" { "prod" } else { "dev" } }}"
        docker build --target "${target}" --tag {{image}} .
    fi

# Run the application (optional: "--docker", "--local", "--prod", "--open").
start *args: stop (_build args)
    #!/usr/bin/env zsh
    instance="$({{just}} _get_instance {{args}})"
    if [ "${instance}" = "--docker" ]; then
        if [ -z "$({{just}} running)" ]; then
            {{check_if_docker_running}}
        fi
        if [[ "{{args}}" = *"--prod"* ]]; then
            docker run \
                --detach \
                --name {{image}} \
                --publish {{prod_port}}:{{prod_port}} \
                --rm \
                {{image}}
            {{ if args =~ "--open" { just + " open --prod" } else { "" } }}
        else
            docker run \
                --detach \
                --name {{image}} \
                --publish {{dev_port}}:{{dev_port}} \
                --rm \
                --volume "$(pwd)"/src:/ui/src \
                {{image}}
            {{ if args =~ "--open" { just +  " open" } else { "" } }}
        fi
    else
        pnpm {{ if args == "--open" { "start" } else { "dev" } }}
    fi

# Stop the server.
stop:
    #!/usr/bin/env zsh
    process=$(pgrep -f {{replace(image, "-", "/")}})
    if [ -n "${process}" ]; then
        echo "${process}" | xargs kill -9
    fi
    if [ -n "$({{just}} running)" ]; then
        docker kill {{image}}
    fi

# Remove the Docker image.
[no-exit-message]
clean:
    #!/usr/bin/env zsh
    if pgrep -q docker; then
        docker rmi {{image}} >/dev/null 2>&1
    fi

# Show the container id (or all info with "--verbose") if the container is running.
running *verbose:
    #!/usr/bin/env zsh
    if pgrep -q docker; then
        docker container ls \
            {{ if verbose =~ "--verbose" { "" } else { "--quiet" } }} \
            --filter name={{image}}
    fi

# Show the Docker logs up to <lines> lines.
logs tail="20":
    #!/usr/bin/env zsh
    if [ -n "$({{just}} running)" ]; then
        docker logs {{image}} --tail {{tail}}
    fi

# Log into the interactive shell in Docker.
shell:
    #!/usr/bin/env zsh
    {{check_if_docker_running}}
    if [ -n "$({{just}} running)" ]; then
        {{just}} start
    fi
    docker exec --interactive --tty {{image}} bash

# Open the application in the browser.
open *prod:
    #!/usr/bin/env zsh
    port={{ if prod == "--prod" { prod_port } else { dev_port } }}
    open "http://localhost:${port}/"
